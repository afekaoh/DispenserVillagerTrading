import org.gradle.api.tasks.SourceSetContainer


plugins {
    id 'java'
    id 'xyz.jpenilla.run-paper' version '2.3.1'
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.21.10-R0.1-SNAPSHOT'
    implementation project(':api') // compile against API
}


tasks.named('jar') {
    dependsOn project(':api').tasks.named('jar')
    from(project(':api').sourceSets.main.output)
    archiveBaseName.set(rootProject.name)
    archiveVersion.set(rootProject.version.toString())
}

// configure the run task (now it exists because the plugin is applied above)
tasks.named('runServer') {
    // optional; run-paper already adds this projectâ€™s jar
    dependsOn tasks.named('jar')
    minecraftVersion("1.21.10")
}



def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}